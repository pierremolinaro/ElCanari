#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#     M E R G E R    D O C U M E N T
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum BoardSide {
  front, back
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum PadShape {
  rectangular, round
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum PadSide {
  traversing, surface
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum PadKind {
  master, slave
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

transient property class MergerViaShapeArray ;
transient property class MergerSegmentArray ;

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

extern outlet class CanariBoardModelView ;
binding CanariBoardModelView $size : transient Int, transient Int ;
binding CanariBoardModelView $zoom : property Int ;
binding CanariBoardModelView $horizontalFlip : transient Bool ;
binding CanariBoardModelView $verticalFlip : transient Bool ;
binding CanariBoardModelView $vias : transient MergerViaShapeArray ;
binding CanariBoardModelView $displayPads : transient Bool ;
binding CanariBoardModelView $displayHoles : transient Bool ;
binding CanariBoardModelView $displayFrontComponentNames : transient Bool ;
binding CanariBoardModelView $frontComponentNameSegments : transient MergerSegmentArray ;
binding CanariBoardModelView $displayBackComponentNames : transient Bool ;
binding CanariBoardModelView $backComponentNameSegments : transient MergerSegmentArray ;
binding CanariBoardModelView $displayFrontTracks : transient Bool ;
binding CanariBoardModelView $frontTracks : transient MergerSegmentArray ;
binding CanariBoardModelView $displayBackTracks : transient Bool ;
binding CanariBoardModelView $backTracks : transient MergerSegmentArray ;

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity BoardModelPadEntity {
  toOne BoardModelPackageEntity myPackage inverse toMany pads ;

  property String name default "" ;
  property Int x default 0 ;
  property Int y default 0 ;
  property Int width default 0 ;
  property Int height default 0 ;
  property Int holeDiameter default 0 ;
  property PadShape shape default rectangular ;
  property PadSide side default traversing ;
  property PadKind kind default master ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity BoardModelComponentNameSegmentEntity {
  toOne BoardModelPackageEntity myPackage inverse toMany componentNameSegments ;

  property Int x1 default 0 ;
  property Int y1 default 0 ;
  property Int x2 default 0 ;
  property Int y2 default 0 ;
  property Int width default 0 ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity BoardModelValueNameSegmentEntity {
  toOne BoardModelPackageEntity myPackage inverse toMany componentValueSegments ;

  property Int x1 default 0 ;
  property Int y1 default 0 ;
  property Int x2 default 0 ;
  property Int y2 default 0 ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity BoardModelPackageEntity {
  toOne BoardModelEntity myModel inverse toMany packages ;
  toMany BoardModelComponentNameSegmentEntity componentNameSegments inverse toOne myPackage ;
  toMany BoardModelValueNameSegmentEntity componentValueSegments inverse toOne myPackage ;
  toMany BoardModelPadEntity pads inverse toOne myPackage ;

  property String name default "" ;
  property Int padRotation default 0 ;
  property BoardSide side default front ;
  
  transient MergerSegmentArray frontComponentNameSegments dependsFrom
    self.componentNameSegments.all.x1, self.componentNameSegments.all.y1,
    self.componentNameSegments.all.x2, self.componentNameSegments.all.y2,
    self.componentNameSegments.all.width,
    self.side
  ;
  
  transient MergerSegmentArray backComponentNameSegments dependsFrom
    self.componentNameSegments.all.x1, self.componentNameSegments.all.y1,
    self.componentNameSegments.all.x2, self.componentNameSegments.all.y2,
    self.componentNameSegments.all.width,
    self.side
  ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity BoardModelViaEntity {
  toOne BoardModelEntity myModel inverse toMany vias ;

  property Int x default 0 ;
  property Int y default 0 ;
  property Int holeDiameter default 0 ;
  property Int padDiameter default 0 ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity BoardModelTrackSegmentEntity {
  toOne BoardModelEntity myModel inverse toMany tracks ;

  property Int x1 default 0 ;
  property Int y1 default 0 ;
  property Int x2 default 0 ;
  property Int y2 default 0 ;
  property Int width default 0 ;
  property BoardSide side default front ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity BoardModelEntity {
  toOne MergerRootEntity myArtwork inverse toMany boardModels ;
  toMany BoardModelTrackSegmentEntity tracks inverse toOne myModel ;
  toMany BoardModelViaEntity vias inverse toOne myModel ;
  toMany BoardModelPackageEntity packages inverse toOne myModel ;
  
  property String artworkName default "" ;
  property String name default "" ;
  property Int boardWidth default 0 ;
  property Int boardWidthUnit default 0 ;
  property Int boardHeight default 0 ;
  property Int boardHeightUnit default 0 ;

  property Int zoom default 100 ;
  property Bool horizontalFlip default no ;
  property Bool verticalFlip default no ;

  property Bool displayPads default yes ;
  property Bool displayHoles default yes ;
  property Bool displayFrontComponentNames default yes ;
  property Bool displayBackComponentNames default yes ;
  property Bool displayFrontTracks default yes ;
  property Bool displayBackTracks default yes ;
  
  copy transient Int viaCount : self.vias.count ;
  copy transient Int componentCount : self.packages.count ;
  
  transient MergerViaShapeArray viaShapes
    dependsFrom self.vias.all.x, self.vias.all.y, self.vias.all.holeDiameter, self.vias.all.padDiameter ;

  transient MergerSegmentArray frontComponentNameSegments dependsFrom
    self.packages.all.frontComponentNameSegments
  ;

  transient Int frontComponentNameSegmentsCount dependsFrom self.frontComponentNameSegments ;
  
  transient MergerSegmentArray backComponentNameSegments dependsFrom
    self.packages.all.backComponentNameSegments
  ;
    
  transient Int backComponentNameSegmentsCount dependsFrom self.backComponentNameSegments ;

  transient MergerSegmentArray backTrackSegments dependsFrom
    self.tracks.all.x1, self.tracks.all.y1,
    self.tracks.all.x2, self.tracks.all.y2,
    self.tracks.all.width, self.tracks.all.side
  ;

  transient Int backTracksSegmentsCount dependsFrom self.backTrackSegments ;

  transient MergerSegmentArray frontTrackSegments dependsFrom
    self.tracks.all.x1, self.tracks.all.y1,
    self.tracks.all.x2, self.tracks.all.y2,
    self.tracks.all.width, self.tracks.all.side
  ;

  transient Int frontTracksSegmentsCount dependsFrom self.frontTrackSegments ;
  
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity MergerRootEntity {
  toMany BoardModelEntity boardModels inverse toOne myArtwork ;
  property Int selectedPageIndex default 0 ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

document PMMergerDocument root MergerRootEntity {
#--- Selected Page
  outlet CanariSegmentedControl mPageSegmentedControl $selectedPage root.selectedPageIndex ;

#--- Add board model
  action addBoardModelAction ;
  outlet EBButton addBoardModelButton $run self.addBoardModelAction ;

#--- Model table view 
  outlet EBTableView mBoardModelTableView $tableValue mBoardModelController ;
  arrayController mBoardModelController {allowsEmptySelection:no, allowsMultipleSelection:no} root.boardModels {
     column "name" sort name EBTextField $value self.name {sendContinously:no}
  }
  selectionController mBoardModelSelection : mBoardModelController.selectedArray ;

#--- Board model name
  outlet EBTextObserverField mBoardModelNameTextField $valueObserver mBoardModelSelection.name ;

#--- Artwork model name
  outlet EBTextObserverField mArtworkNameTextField $valueObserver mBoardModelSelection.artworkName ;

#--- Model width display
  outlet EBPopUpButton mBoardWidthUnitPopUp $selectedTag mBoardModelSelection.boardWidthUnit ;
  outlet CanariDimensionTextField mBoardWidthTextField
     $dimensionAndUnit mBoardModelSelection.boardWidth, mBoardModelSelection.boardWidthUnit
  ;

#--- Model Height display
  outlet EBPopUpButton mBoardHeightUnitPopUp $selectedTag mBoardModelSelection.boardHeightUnit ;
  outlet CanariDimensionTextField mBoardHeightTextField
     $dimensionAndUnit mBoardModelSelection.boardHeight, mBoardModelSelection.boardHeightUnit
  ;

#--- Board model via count
  outlet EBIntObserverField mBoardModelViaCountTextField $valueObserver mBoardModelSelection.viaCount {autoFormatter : yes} ;

#--- Board model component count
  outlet EBIntObserverField mBoardModelComponentCountTextField $valueObserver mBoardModelSelection.componentCount {autoFormatter : yes} ;

#--- Front component names segment count
  outlet EBIntObserverField mFrontComponentNameSegmentCountTextField $valueObserver mBoardModelSelection.frontComponentNameSegmentsCount {autoFormatter:yes} ;

#--- Front track segment count
  outlet EBIntObserverField mFrontTrackSegmentCountTextField $valueObserver mBoardModelSelection.frontTracksSegmentsCount {autoFormatter:yes} ;

#--- Back track segment count
  outlet EBIntObserverField mBackTrackSegmentCountTextField $valueObserver mBoardModelSelection.backTracksSegmentsCount {autoFormatter:yes} ;

#--- Back component names segment count
  outlet EBIntObserverField mBackComponentNameSegmentCountTextField $valueObserver mBoardModelSelection.backComponentNameSegmentsCount {autoFormatter:yes} ;

#--- Model view
  outlet CanariBoardModelView mBoardModelView
    $size mBoardModelSelection.boardWidth, mBoardModelSelection.boardHeight
    $zoom mBoardModelSelection.zoom
    $horizontalFlip mBoardModelSelection.horizontalFlip
    $verticalFlip mBoardModelSelection.verticalFlip
    $vias mBoardModelSelection.viaShapes
    $displayPads mBoardModelSelection.displayPads
    $displayHoles mBoardModelSelection.displayHoles
    $displayFrontComponentNames mBoardModelSelection.displayFrontComponentNames
    $frontComponentNameSegments mBoardModelSelection.frontComponentNameSegments
    $displayBackComponentNames mBoardModelSelection.displayBackComponentNames
    $backComponentNameSegments mBoardModelSelection.backComponentNameSegments
    $displayFrontTracks mBoardModelSelection.displayFrontTracks
    $frontTracks mBoardModelSelection.frontTrackSegments
    $displayBackTracks mBoardModelSelection.displayBackTracks
    $backTracks mBoardModelSelection.backTrackSegments
  ;
  
  outlet EBSwitch mHorizontalFlipSwitch $value mBoardModelSelection.horizontalFlip ;
  outlet EBSwitch mVerticalFlipSwitch $value mBoardModelSelection.verticalFlip ;
  
  outlet EBCheckedMenuItem mDisplayPads $checked mBoardModelSelection.displayPads ;
  outlet EBCheckedMenuItem mDisplayHoles $checked mBoardModelSelection.displayHoles ;
  outlet EBCheckedMenuItem mDisplayFrontComponentNames $checked mBoardModelSelection.displayFrontComponentNames ;
  outlet EBCheckedMenuItem mDisplayBackComponentNames $checked mBoardModelSelection.displayBackComponentNames ;
  outlet EBCheckedMenuItem mDisplayFrontTracks $checked mBoardModelSelection.displayFrontTracks ;
  outlet EBCheckedMenuItem mDisplayBackTracks $checked mBoardModelSelection.displayBackTracks ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
