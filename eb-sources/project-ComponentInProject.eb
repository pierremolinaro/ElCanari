//———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

graphic entity ComponentInProject : BoardObject {
  toOne DeviceInProject mDevice inverse toMany mComponents ;
  toOne DevicePackageInProject mSelectedPackage ;
  toMany ComponentSymbolInProject mSymbols inverse toOne mComponent ;

  property String mNamePrefix default "" ;
  property Int mNameIndex default 0 ;
  property String mComponentValue default "" ;
  
  property Int mX default 0 ;
  property Int mXUnit default 2_286 ; // mils
  property Int mY default 0 ;
  property Int mYUnit default 2_286 ; // mils
  property Int mRotation default 0 ; // 1000 -> 1°

  transient String componentName dependsFrom self.mNamePrefix, self.mNameIndex ;
  transient String deviceName dependsFrom self.mDevice.one.mDeviceName ;
  transient String selectedPackageName dependsFrom self.mSelectedPackage.one.mPackageName ;

  transient StringArray availablePackages dependsFrom self.mDevice.one.packageNames ;

  transient StringTagArray unplacedSymbols dependsFrom
    self.componentName,
    self.mSymbols,
    self.mSymbols.all.symbolInSchematic,
    self.mSymbols.all.mSymbolInstanceName,
    self.mSymbols.all.mSymbolTypeName
  ;

  transient Bool componentIsPlacedInBoard dependsFrom super.BoardObject.isPlacedInBoard ;

  transient DeviceSymbolDictionary deviceSymbolDictionary dependsFrom self.mDevice.one.deviceSymbolDictionary ;

  transient String placementInSchematic dependsFrom self.mSymbols.all.symbolInSchematic ;
  
  transient NSBezierPath strokeBezierPath dependsFrom self.mSelectedPackage.one.mStrokeBezierPath ;

  transient PackagePadDictionary padDictionary dependsFrom self.mSelectedPackage.one.padDictionary ;
  
  override transient EBShape objectDisplay dependsFrom
    self.mX, self.mY, self.mRotation, self.strokeBezierPath,
    self.padDictionary,
    prefs.frontSideLegendColorForBoard,
    prefs.packageDrawingWidthMultpliedByTenForBoard,
    prefs.frontSidePadColorForBoard,
    super.BoardObject.displayFrontPads,
    prefs.backSidePadColorForBoard,
    super.BoardObject.displayBackPads
  ;

  override transient EBShape selectionDisplay dependsFrom
    self.mX, self.mY, self.mRotation, self.strokeBezierPath,
    self.padDictionary
  ;
}

//———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
